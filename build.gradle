buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessVersion"
    }
}


allprojects {
    /// Plugins
    // Compilation
    apply plugin: "java"
    apply plugin: "kotlin"

    // Documentation
    apply plugin: "org.jetbrains.dokka"

    // Static analysis
    apply plugin: "com.diffplug.gradle.spotless"

    /// Dependencies
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }
        maven { url "https://soot-build.cs.uni-paderborn.de/nexus/repository/soot-release/" }
    }

    dependencies {
        compile "com.github.cafejojo:schaapi:master-SNAPSHOT"

        compile group: "ca.mcgill.sable", name: "soot", version: sootVersion, {
            exclude group: "org.slf4j", module: "slf4j-simple"
        }
        implementation group: "io.github.microutils", name: "kotlin-logging", version: kotlinLoggingVersion

        testCompile group: "com.nhaarman", name: "mockito-kotlin", version: mockitoKotlinVersion
        testCompile group: "org.assertj", name: "assertj-core", version: assertjVersion
        testCompile group: "org.jetbrains.spek", name: "spek-api", version: spekVersion
        testRuntime group: "org.jetbrains.spek", name: "spek-junit-platform-engine", version: spekVersion
        testCompile group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion

        moduleIntegrationTestCompile project(":models:jimple-library-usage-graph")
    }

    /// Configuration
    // Kotlin
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    // JUnit / Spek
    test {
        testLogging {
            exceptionFormat = "full"
        }

        useJUnitPlatform() {
            includeEngines "junit-jupiter", "spek"
        }
    }

    // Spotless
    spotless {
        kotlin {
            ktlint()
        }
    }
}

// Global config
apply from: "gradle/detekt.gradle"
